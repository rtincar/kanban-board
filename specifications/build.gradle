description = "Acceptance Tests for the BDD cycle (objective: have conversaions and demonstrate requirements)"

ext {
    spock_version = '1.1-groovy-2.4'
}

buildscript {
    ext.spring_boot_version = '2.0.0.M6'

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
    }
}

apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'

test {

    // make gradle print the test result for each test in the build (we like to see the acceptance tests running)
    testLogging {
        events "passed", "skipped", "failed"
    }

}

// we want to run the acceptance tests after the unit tests, to follow the testing pyramid
test.mustRunAfter(
        ":application:configuration:test",
        ":application:core:test",
        ":application:data:test",
        ":application:web:test"
)

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.12'
    compile project(":application:configuration")
    testCompile "org.spockframework:spock-core:$spock_version"
    testCompile "org.spockframework:spock-spring:$spock_version"
    testCompile "org.springframework.boot:spring-boot-starter-test:$spring_boot_version"
    testCompile 'io.projectreactor:reactor-test:3.1.0.RELEASE'
    testCompile( 'com.athaydes:spock-reports:1.3.2' ) {
        transitive = false
    }
}



